module LocalMonitor;

export *;
import * from Architecture;
import * from Param;

interface LocalMonitorInterface{
  Unit check();
  Unit tick();
  Unit printReqs();
}

class LocalMonitor(LoadBalancer balancer, String balancerName, Rat mcl, Int baseInstanceN, Rat mf, List<Int> predictions) implements LocalMonitorInterface {
  Int deplInstances = baseInstanceN;
  ScalingWrapper sw;
  Rat k = k() * mf;
  Rat kbig =  kbig() * mf;
  Int i = 0;
  List<Int> reqs = list[];

  {sw = new ScalingWrapperImpl(balancer);}

  Unit tick() {balancer.tick();}

  Unit printReqs() {println(balancerName + ": " + toString(reqs));}

  Int getTarget() {
    Int max = 0;
    if(isEmpty(predictions)) {
      max = balancer.get_one_sec_workload();
      reqs = appendright(reqs,max);
    } else {
      max = nth(predictions,i);
      if(i < length(predictions)) i = i + 1;
    }
    return max;
  }

  Unit check() {
    Int max = this.getTarget();
    if(max - (mcl * deplInstances - kbig)  > k || (mcl * deplInstances - kbig) - max > k) {
      Int configInstances = ceil(float((max + kbig)/ mcl));
      if(configInstances > deplInstances) {sw!deploy(configInstances - deplInstances);}
      else if(configInstances < deplInstances && deplInstances >= baseInstanceN) {sw!undeploy(deplInstances - configInstances);}
      deplInstances = configInstances;
    }
    balancer.resetMetrics();
  }
}

interface ScalingWrapper {
  Unit deploy(Int n);
  Unit undeploy(Int n);
}

class ScalingWrapperImpl(LoadBalancer balancer) implements ScalingWrapper{
  Unit deploy(Int n) {
    Int times = n;
    while(times != 0) {
      balancer!deploy();
      times = times - 1;
    }
  }

  Unit undeploy(Int n) {
    Int times = n;
    while(times != 0) {
      balancer!undeploy();
      times = times - 1;
    }
  }
}